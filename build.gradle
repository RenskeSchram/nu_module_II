/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'hendrik.vanderlinde' at '3/3/17 2:12 PM' with Gradle 2.10
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.10/userguide/tutorial_java_projects.html
 */

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.hidetake.ssh" version "2.10.1"
    id 'java'
    id 'application'
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
}

test {
    useJUnitPlatform()
    maxHeapSize = '1G'
}

// Global settings
ssh.settings {
    knownHosts = allowAnyHosts // Disable host key verification
}

mainClassName = 'com.nedap.university.Main'
version = '0.0.1'

jar {
    manifest {
        attributes 'Implementation-Title': 'Nedap University Module 2 Sample Project',
                'Implementation-Version': archiveVersion,
                'Main-Class': mainClassName
    }
}

remotes {
    pi {
        role 'masterNode'
        host = '172.16.1.1'
        user = 'pi'
        password = 'raspberry'
    }
}

task status {
    doLast {
        ssh.run {
            session(remotes.pi) {
                execute 'sudo systemctl status num2.service'
            }
        }
    }
}

task start {
    doLast {
        ssh.run {
            session(remotes.pi) {
                execute 'sudo systemctl start num2.service'
            }
        }
    }
}

task stop {
    doLast {
        ssh.run {
            session(remotes.pi) {
                execute 'sudo systemctl stop num2.service'
            }
        }
    }
}

task deploy {
    doLast {
        ssh.run {
            session(remotes.pi) {
                put from: "${project.rootDir}/build/libs/NUM2-${version}.jar", into: "/home/pi/NUM2.jar"
                execute 'sudo systemctl restart num2.service'
            }
        }
    }
}

run {
    standardInput = System.in
}

deploy.dependsOn jar
